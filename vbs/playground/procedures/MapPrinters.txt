Sub MapPrinter(strLDAPUrl,strPrintServer)
	'
	' Maps printer based on what OU the computer is in
	' This works if the printer object has been moved into the OU
	'
	' Based on our naming convention, the default printer is a laser printer
	' this information is pulled from the printer object itself.
	'
	On Error Resume Next
	Dim objNetwork
	Dim objItem
	Dim colItems
	Dim strPrinter
	
	Set objNetwork = CreateObject("Wscript.Network")
	Set colItems = GetObject("LDAP://" & strLDAPUrl)
	
	For Each objItem in colItems
		If Left(objItem.CN, 3) = "PS-" Then
			strPrinter = Right(objItem.CN, Len(objItem.CN) - 3)
			If InStr(objItem.CN, "laser") Then
				objNetwork.AddWindowsPrinterConnection "\\" & strPrintServer & "\" & strPrinter
				objNetwork.SetDefaultPrinter "\\" & strPrintServer & "\" & strPrinter
			Else
				objNetwork.AddWindowsPrinterConnection "\\" & strPrintServer & "\" & strPrinter
			End If
		Else
		End If	
	Next
End Sub
